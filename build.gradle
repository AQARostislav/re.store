plugins {
    id 'java-library'
    id 'io.qameta.allure' version '3.0.1'
}

allure {
    report {
        version.set("2.32.0")
    }
    adapter {
        aspectjWeaver.set(true)
        frameworks {
            junit5 {
                adapterVersion.set("2.32.0")
            }
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.13.4')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'com.codeborne:selenide:7.14.0'
    testImplementation "io.qameta.allure:allure-junit5:2.29.0"
    testImplementation "io.qameta.allure:allure-selenide:2.29.0"
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation 'org.slf4j:slf4j-simple:2.0.16'
}

tasks.withType(Test) {
    useJUnitPlatform() // Указание использовать JUnit Platform для запуска тестов

    testLogging {
        lifecycle {
            events "started", "skipped", "failed", "standard_error", "standard_out"
// Конфигурация логирования тестов: какие события логировать
            exceptionFormat "short" // Форматирование исключений в коротком виде
        }
    }
}
tasks.register("regression_test", Test) {
    useJUnitPlatform {
        includeTags("regression")
    }
    // обязательно, чтобы Gradle видел тесты
    testClassesDirs = sourceSets.test.output.classesDirs
    classpath = sourceSets.test.runtimeClasspath
    // чтобы тесты точно компилились перед запуском
    dependsOn testClasses
    // (опционально) лог выводить нормальный
    testLogging {
        events "passed", "skipped", "failed"
    }
}

